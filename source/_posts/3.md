---
title: Ethernautの問題を解いてみる【1.Fallback】
date: 2020-07-12 00:48:26
categories:
  - Solidity
---

### はじめに

最近、主催している福岡ブロックチェーン勉強会で、[Ethernaut](https://solidity-05.ethernaut.openzeppelin.com/)というサイトの問題を解いています。

お題のEthereumのスマートコントラクトのコードに対して

条件を満たすと、クリアというサイトです。

問題の内容としては、過去にEthereumで見つかった有名な脆弱性などもあります。


今回は、そんなEthernautの1問目の問題を解いてみました。


<br />
<br />

### 1. Fallback の問題

URLは[こちら](https://solidity-05.ethernaut.openzeppelin.com/level/0xD95B091f19946d6ef0c88f8CD360c0d6E408876E)ですね。


<br />
<br />

##### お題
{% codeblock %}
You will beat this level if
  1. you claim ownership of the contract
  2. you reduce its balance to 0

Things that might help
  - How to send ether when interacting with an ABI
  - How to send ether outside of the ABI
  - Converting to and from wei/ether units -see help() command-
  - Fallback methods
{% endcodeblock %}

要は
- コントラクトのオーナーシップを奪う
- コントラクトの残高を0にする

を満たすことができれば、クリアですね。

<br />
<br />

##### スマートコントラクト

{% codeblock lang:javascript %}
pragma solidity ^0.5.0;

import 'openzeppelin-solidity/contracts/math/SafeMath.sol';

contract Fallback {

  using SafeMath for uint256;
  mapping(address => uint) public contributions;
  address payable public owner;

  constructor() public {
    owner = msg.sender;
    contributions[msg.sender] = 1000 * (1 ether);
  }

  modifier onlyOwner {
        require(
            msg.sender == owner,
            "caller is not the owner"
        );
        _;
    }

  function contribute() public payable {
    require(msg.value < 0.001 ether);
    contributions[msg.sender] += msg.value;
    if(contributions[msg.sender] > contributions[owner]) {
      owner = msg.sender;
    }
  }

  function getContribution() public view returns (uint) {
    return contributions[msg.sender];
  }

  function withdraw() public onlyOwner {
    owner.transfer(address(this).balance);
  }

  function() payable external {
    require(msg.value > 0 && contributions[msg.sender] > 0);
    owner = msg.sender;
  }
}
{% endcodeblock %}

<br />
<br />

Fallbackというスマートコントラクトになっています。

<br />
<br />

### オーナーを奪う方法を考える

まずは、「オーナーシップを奪う」必要があります。

コードをみる限り、オーナーになる方法は二つ。

<br />
<br />

##### 一つ目

一つ目は以下の部分で。

{% codeblock lang:javascript %}
function contribute() public payable {
    require(msg.value < 0.001 ether);
    contributions[msg.sender] += msg.value;
    if(contributions[msg.sender] > contributions[owner]) {
      owner = msg.sender;
    }
  }
{% endcodeblock %}


**contributions[msg.sender]** が **contributions[owner]**より多ければ、

**msg.sender** つまりコントラクトを実行した人がオーナーになることができます。

<br />
<br />

しかし、**constructor**をみると、これは現実的でないことがわかります。

{% codeblock lang:javascript %}
constructor() public {
    owner = msg.sender;
    contributions[msg.sender] = 1000 * (1 ether);
}
{% endcodeblock %}

コントラクトが作成された時点で、**contributions[owner]** は **1000 * (1 ether)**となっていることがわかります。

**require(msg.value < 0.001 ether);**が存在するため、**contribute()**では、一度に0.001 ether未満の額しか増やすことができません。

オーナーよりcontributionsを多くすることで、オーナー権限を奪うことは難しそうです。

<br />
<br />

##### 二つ目

二つ目に以下の部分があります。

{% codeblock lang:javascript %}
function() payable external {
    require(msg.value > 0 && contributions[msg.sender] > 0);
    owner = msg.sender;
}
{% endcodeblock %}

こちらは比較的簡単にオーナーを奪うことができそうです。

**require(msg.value > 0 && contributions[msg.sender] > 0);**

ここを満たすことができれば、オーナーを奪うことができます。


<br />
<br />

### 実際にオーナーを奪ってみる


##### contribute()
まずは、contributionsを0より大きくする必要があるので、**contribute()**を実行します。

![Get New Instance](/images/3/getNewInstance.png)

ボタンをクリックして、Instanceを生成。


chromeのディベロッパーツールのconsoleから、**contribute()**を実行します。

**require(msg.value < 0.001 ether);**　の条件がありますので、0.001etherより小さい値を送る必要があります。

非同期処理なので、**await**をつけるのを忘れずに。

{% codeblock lang:javascript %}
await contract.contribute({value: toWei("0.0001", "ether"), to: contract, from: player})
{% endcodeblock %}

<br />
<br />

##### getContribution()で確認

現在のcontributionsの数を**getContribution()**で確認できます。

{% codeblock lang:javascript %}
await contract.getContribution()
{% endcodeblock %}

![getContribution](/images/3/getContribution.png)

**words**に値が入っていることが確認できました。

<br />
<br />

##### sendTransaction()

contributionsの値が0より大きくなったことが確認できました。

これで、sendTransaction()を実行できるはずです。

以下のように実行してみます。

{% codeblock lang:javascript %}
await contract.sendTransaction({value: toWei("0.01", "ether"), to: instance, from: player})
{% endcodeblock %}


うまく実行できたら、これでオーナー権限を奪えているはずです。

<br />
<br />


##### ownerを確認する

オーナーを奪えたかどうか確認します。

![owner](/images/3/owner.png)

オーナーのアドレスとplayerのアドレスが一致していることが確認できます。


これでオーナーを奪うことができました。

<br />
<br />


### withdraw()を実行する


最後に

```
2. you reduce its balance to 0
```

を達成するために、**withdraw()**を実行します。


{% codeblock lang:javascript %}
function withdraw() public onlyOwner {
    owner.transfer(address(this).balance);
}
{% endcodeblock %}

オーナーしか実行できないfunctionになっています。

しかしオーナー権は奪っているので、実行できるはず。

{% codeblock lang:javascript %}
await contract.withdraw()
{% endcodeblock %}

を実行します。

<br />
<br />

##### コントラクトの残高を確認

しっかり0になっているかどうか、確認します。

{% codeblock lang:javascript %}
await getBalance(instance)
{% endcodeblock %}


![getBalance](/images/3/getBalance.png)

0になっていることが確認できました。


最後にSubmit Instanceをすると、

こんな画面がでてきたら、問題クリアです！

![complete](/images/3/complete.png)

<br />
<br />

### 最後に


今回はEthernautの1問目Fallbackを解いてみました。

最初の問題ということもあり、難易度は低めでしたね。

次回は2問目を解いていけたらと思います。


最後まで読んでいただき、ありがとうございました！

<br />
<br />
<br />
<br />


-----------------------------------------------------
<br />
<br />

[株式会社Fusic](https://fusic.co.jp/)では一緒に働くエンジニアを募集しています！

福岡での就職・転職に少しでも興味のある方は、

[ayasamind宛のDM](https://twitter.com/ayasamind)、[Fusic公式サイトのお問い合わせ](https://fusic.co.jp/contact/)など、お気軽にご相談ください。

<br />
<br />
